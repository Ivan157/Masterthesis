# Program 2(a):  Poisson likelihood, log link, Random Effects (Dietary fat example) 
# Poisson likelihood, log link 
# Random effects model for multi-arm trials 

model { 
  for(i in 1:ns){                 # LOOP THROUGH STUDIES 
    w[i,1] <- 0                 # adjustment for multi-arm trials is zero for control arm 
    delta[i,1] <- 0             # treatment effect is zero for control arm 
    mu[i] ~ dnorm(0,.0001)      # vague priors for all trial baselines 
    
    for (k in 1:na[i]) {            # LOOP THROUGH ARMS 
      r[i,k] ~ dpois(theta[i,k])  # Poisson likelihood 
      theta[i,k] <- lambda[i,k]*E[i,k]            # failure rate * exposure 
      log(lambda[i,k]) <- mu[i] + delta[i,k]      # model for linear predictor 
      dev[i,k] <- 2*((theta[i,k]-r[i,k]) + r[i,k]*log(r[i,k]/theta[i,k]))     #Deviance contribution 
    } 
    resdev[i] <- sum(dev[i,1:na[i]])        # summed residual deviance contribution for this trial 
    for (k in 2:na[i]) {                    # LOOP THROUGH ARMS 
      delta[i,k] ~ dnorm(md[i,k],taud[i,k])           # trial-specific LOR distributions 
      md[i,k] <- d[t[i,k]] - d[t[i,1]] + sw[i,k]      # mean of LOR distributions (with multi-arm trial correction
      taud[i,k] <- tau *2*(k-1)/k                     # precision of LOR distributions (with multi-arm trial correction
      w[i,k] <- (delta[i,k] - d[t[i,k]] + d[t[i,1]]) 				# adjustment for multi-arm RCTs 
      sw[i,k] <- sum(w[i,1:(k-1)])/(k-1) 				# cumulative adjustment for multi-arm trials 
    } 
  }    
  totresdev <- sum(resdev[1:10])          #Total Residual Deviance 
  d[1]<-0                             # treatment effect is zero for reference treatment 
  for (k in 2:nt){ d[k] ~ dnorm(0,.0001) }    # vague priors for treatment effects 
  sd ~ dunif(0,5)                             # vague prior for between-trial SD 
  tau <- pow(sd,-2)                   # between-trial precision = (1/between-trial variance) 
  
  # zusätzlich eingefügt
  A ~ dnorm(-3,1.77) 
  for (k in 1:nt) { log(T[k]) <- A + d[k]  }
} # Model END

