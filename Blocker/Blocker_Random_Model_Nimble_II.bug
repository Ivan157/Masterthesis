# Binomial likelihood, logit link 
# Random effects model for multi-arm trials 

model{								 # *** PROGRAM STARTS 
for(i in 1:ns){								 # LOOP THROUGH STUDIES 
 w[i,1] <- 0 								# adjustment for multi-arm trials is zero for control arm 
 delta[i,1] <- 0 								# treatment effect is zero for control arm 
 mu[i] ~ dnorm(0,.0001) 				# vague priors for all trial baselines 
 for (k in 1:na[i]) { 								# LOOP THROUGH ARMS 
 r[i,k] ~ dbin(p[i,k],n[i,k]) 				# binomial likelihood 
 logit(p[i,k]) <- mu[i] + delta[i,k]				 # model for linear predictor 
 rhat[i,k] <- p[i,k] * n[i,k] 				# expected value of the numerators 
 dev[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhat[i,k])) #Deviance contribution 
            +  (n[i,k]-r[i,k]) * (log(n[i,k]-r[i,k]) - log(n[i,k]-rhat[i,k])))  
      } 
 resdev[i] <- sum(dev[i,1:na[i]]) 				# summed residual deviance contribution for this trial 
 for (k in 2:na[i]) { 								# LOOP THROUGH ARMS 
          delta[i,k] ~ dnorm(md[i,k],taud[i,k]) 				# trial-specific LOR distributions 
 md[i,k] <- d[t[i,k]] - d[t[i,1]] + sw[i,k] 				# mean of LOR distributions (with multi-arm trial correction) 
 taud[i,k] <- tau *2*(k-1)/k 								# precision of LOR distributions (with multi-arm trial correction) 
 w[i,k] <- (delta[i,k] - d[t[i,k]] + d[t[i,1]]) 				# adjustment for multi-arm RCTs 
 sw[i,k] <- sum(w[i,1:(k-1)])/(k-1) 				# cumulative adjustment for multi-arm trials; Modifikation
         } 
  }    
totresdev <- sum(resdev[1:22]) 								#Total Residual Deviance; Modifikation

d[1] <- 0												 # treatment effect is zero for reference treatment 
sd ~ dunif(0,5) 												# vague prior for between-trial SD.
tau <- pow(sd,-2) 								# between-trial precision = (1/between-trial variance) 

# Provide estimates of treatment effects T[k] on the natural (probability) scale  
# Given a Mean Effect, meanA, for ‘standard’ treatment 1, with precision (1/variance) precA 


for (k in 2:nt){ d[k] ~ dnorm(0,0.0001) }
for (k in 1:nt) { logit(T[k]) <- A + d[k] }
A ~ dnorm(-2.2, 3.3) 
}   # Model END

